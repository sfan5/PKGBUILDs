# U-Boot: ODROID-C4
# Maintainer: sfan5 <sfan5@live.de>

# Note: must be built on x86 with an old cross toolchain

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c4
pkgver=2015.01
pkgrel=1
pkgdesc="U-Boot for ODROID-C4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('bc')
backup=('boot/boot.ini')
_commit=766167bbe787e494e47376b31cd017b897e9594c
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        'boot.ini'
        '0001-sd_fusing-tweaks.diff'
        '0002-arch-linux-arm-modifications.diff')
sha1sums=('9e2dc16145138e8e3801df44cdd617eeb8e596e8'
          'bb2f736322c113594ae12d792e63deb1a5d5f774'
          '0a9aac03daaa4ac1929d6b4ef33569ab93eb7804'
          '43f3e1f5ba825d16a51fe66e2169146d9b421f4d')

prepare() {
  cd u-boot-${_commit}

  patch -p0 --verbose <../0001-sd_fusing-tweaks.diff
  patch -p0 --verbose <../0002-arch-linux-arm-modifications.diff
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make distclean
  make odroidc4_config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp sd_fusing.sh u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}"/boot.ini "${pkgdir}"/boot
}
